/** Absolute minimum build to be able to publish when the plugin-plugin is not available */
import org.gradle.api.publish.maven.MavenPublication

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2' }
}

repositories { jcenter() }

apply plugin: 'groovy'
apply plugin: 'idea'

group = 'com.netflix.nebula'

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

apply plugin: 'com.jfrog.bintray'
def packageName = name
bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'nebula'
        name = packageName
        licenses = ['Apache-2.0']
    }
    // Will have to manually publish on bintray.com
}
bintrayUpload.dependsOn(build)

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installed $project.name"
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.netflix.nebula:gradle-scm-plugin:2.4.0-SNAPSHOT'
    testCompile('com.netflix.nebula:nebula-test:2.4.0-SNAPSHOT') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'com.netflix.nebula:gradle-git-scm-plugin:2.4.0-SNAPSHOT'
    testCompile 'org.ajoberstar:grgit:0.2.2'
}

task createWrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
